# Query Shape Rules

# One or more operators makes up a query
root        <- _space_* query _space_*
query       <- (operator _space_*)+

# There are realy only two kinds of operator: include and exclude
operator    <- excluding
                / including

# To exclude, you use a "-" before the query. OR cannot be negated unless it's in a group.
excluding   <- "-" base
including   <- "" (or / base)

# Operators can be grouped with an OR keyword in groups of two or more.
# I'm not sure if this is the ideal way to do this with PEG but it works for now.
or          <- orable or_groups
or_groups   <- (or_sep orable)+
orable      <- "" base

# The base operators are the things that make up the query itself.
# Filters, hashtags, text etc.
base        <- group
                / url
                / pair
                / exact
                / word

# A group is a complete subquery. Recursion!
group       <- "(" root ")"

# Query Syntax Rules
# These outline the specifics of the syntax

url         <- slug url_sep v:word
pair        <- slug sep v:word
exact       <- '"' ([^\"]*) '"'
word        <- "+"? [^\s\)\(]+

or_sep      <- _space_+ "OR" _space_+
# There's probably a better way to do this
date        <- d d d d "-" d d "-" d d
# This is clearly wrong
slug        <- [a-zA-Z0-9_]+
url_sep     <- "://"
sep         <- ":"
integer     <- d d*
d           <- [0-9]
_space_     <- [\s]